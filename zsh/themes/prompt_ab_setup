#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#     Steve Losh <steve@stevelosh.com>
#     Bart Trojanowski <bart@jukie.net>
#     Brian Carper <brian@carper.ca>
#     steeef <steeef@gmail.com>
#     Sorin Ionescu <sorin.ionescu@gmail.com>
#     Yan Pritzker <yan@pritzker.ws>
#
# Screenshots:
#     http://i.imgur.com/gLgVp6Y.png
#

set_node_version() {
    # Empty
    node_version=
    # Show NVM status only for JS-specific folders
    [[ -f package.json || -d node_modules || $(ls -1f "*.js" 2>/dev/null | wc -l) -gt 0 ]] || return
    if (( $+functions[nvm_version] )); then
      node_version="⬢ ${$(nvm_version)#v}"
    fi
    true
}

set_php_version() {

}

set_git_info() {
    if (( $+functions[git-info] )); then
        git-info
    fi
}

prompt_ab_precmd() {
    # Set Git repository information.
    set_git_info

    # Update node_version variable, if not updated, update PHP version variable.
    set_node_version || set_php_version
}

prompt_ab_setup() {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent subst)

    # Load required functions.
    autoload -Uz add-zsh-hook

    # Add hook to set up prompt parameters before each command.
    add-zsh-hook precmd prompt_ab_precmd

    # Use extended color pallete if available.
    if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
        _prompt_ab_colors=(
            "%F{81}"    # Turquoise
            "%F{166}" # Orange
            "%F{135}" # Purple
            "%F{161}" # Hotpink
            "%F{118}" # Limegreen
        )
    else
        _prompt_ab_colors=(
            "%F{cyan}"
            "%F{yellow}"
            "%F{magenta}"
            "%F{red}"
            "%F{green}"
        )
    fi

    # Define prompts.
    local lprompt=""

    # USER_INFO="${_prompt_ab_colors[3]}%n%f@${_prompt_ab_colors[2]}%m%f ${_prompt_ab_colors[5]}%~%f "
    # show username@host if logged in through SSH
    [[ "$SSH_CONNECTION" != '' ]] && lprompt+=' %F{242}%n@%m%f '

    # show username@host if root, with username in white
    [[ $UID -eq 0 ]] && lprompt=+' %F{white}%n%f%F{242}@%m%f '

    # Show directory
    lprompt+="${_prompt_ab_colors[5]}%~%f "

    # Git info
    zstyle ':prezto:module:git:info' verbose 'yes'
    zstyle ':prezto:module:git:info:branch' format "${_prompt_ab_colors[1]}%b%f"
    zstyle ':prezto:module:git:info:added' format "${_prompt_ab_colors[5]}●%f"
    zstyle ':prezto:module:git:info:deleted' format "${_prompt_ab_colors[2]}●%f"
    zstyle ':prezto:module:git:info:modified' format "${_prompt_ab_colors[4]}●%f"
    zstyle ':prezto:module:git:info:untracked' format "${_prompt_ab_colors[3]}●%f"
    zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)' # <= Format to show git info in
    lprompt+='$git_info[prompt] '

    # Prompt symbol
    lprompt+="%(?.${_prompt_ab_colors[3]}.${_prompt_ab_colors[4]})❯%f "

    # All set for the left prompt.
    PROMPT=${lprompt}
    RPROMPT='%F{blue}${node_version}${php_version}'
}

prompt_ab_setup "$@"
