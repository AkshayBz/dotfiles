#
# Components from: Skwp, Spaceship & Pure.
#

set_node_version() {
    # Reset
    node_version=
    # Show NVM status only for JS-specific folders
    # $(ls -1f *.js 2>/dev/null | wc -l) -gt 0 # Disabling extension check until I fix perf.
    [[ -f package.json || -f .nvmrc || -d node_modules ]] || return
    if (( $+functions[nvm_version] )); then
        node_version="‚¨¢ ${$(nvm_version)#v}"
        true
    fi
}

set_php_version() {
    # Reset
    php_version=
    # Show PHPBrew status only for PHP-specific folders
    # $(ls -1f *.php 2>/dev/null | wc -l) -gt 0 # Disabling extension check until I fix perf.
    [[ -f 'composer.json' ]] || return
    if (( $+functions[phpbrew_current_php_version] )); then
        php_version=`echo $(phpbrew_current_php_version) | sed -e 's/php://' | sed -e 's/-phpbrew//'`
        php_version="üëΩ  ${php_version}"
        true
    fi
}

set_git_info() {
    if (( $+functions[git-info] )); then
        git-info
    fi
}

prompt_ab_precmd() {
    # Set Git repository information.
    set_git_info

    # Update node_version variable, if not updated, update PHP version variable.
    set_node_version || set_php_version
}

prompt_ab_setup() {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent subst)

    # Load required functions.
    autoload -Uz add-zsh-hook

    # Add hook to set up prompt parameters before each command.
    add-zsh-hook precmd prompt_ab_precmd

    # Use extended color pallete if available.
    if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
        _prompt_ab_colors=(
            "%F{81}"    # Turquoise
            "%F{166}" # Orange
            "%F{135}" # Purple
            "%F{161}" # Hotpink
            "%F{118}" # Limegreen
        )
    else
        _prompt_ab_colors=(
            "%F{cyan}"
            "%F{yellow}"
            "%F{magenta}"
            "%F{red}"
            "%F{green}"
        )
    fi

    # Define prompts.
    local lprompt=""

    # USER_INFO="${_prompt_ab_colors[3]}%n%f@${_prompt_ab_colors[2]}%m%f ${_prompt_ab_colors[5]}%~%f "
    # show username@host if logged in through SSH
    [[ "$SSH_CONNECTION" != '' ]] && lprompt+=' %F{242}%n@%m%f '

    # show username@host if root, with username in white
    [[ $UID -eq 0 ]] && lprompt=+' %F{white}%n%f%F{242}@%m%f '

    # Show directory
    lprompt+="${_prompt_ab_colors[5]}%~%f "

    # Git info
    zstyle ':prezto:module:git:info' verbose 'yes'
    zstyle ':prezto:module:git:info:branch' format "${_prompt_ab_colors[1]}%b%f"
    zstyle ':prezto:module:git:info:added' format "${_prompt_ab_colors[5]}‚óè%f"
    zstyle ':prezto:module:git:info:deleted' format "${_prompt_ab_colors[2]}‚óè%f"
    zstyle ':prezto:module:git:info:modified' format "${_prompt_ab_colors[4]}‚óè%f"
    zstyle ':prezto:module:git:info:untracked' format "${_prompt_ab_colors[3]}‚óè%f"
    zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)' # <= Format to show git info in
    lprompt+='$git_info[prompt] '

    # Prompt symbol ‚ùØ with colors
    lprompt+="%(?.${_prompt_ab_colors[3]}.${_prompt_ab_colors[4]})‚ùØ%f "

    # All set for the left prompt.
    PROMPT=${lprompt}

    # Do the right prompt
    local rprompt='%F{green}${node_version}%f' # Node version in Green
    rprompt+='%F{blue}${php_version}%f' # PHP Version in Blue

    # RPROMPT='%F{118}${node_version}${php_version}'
    # All set for right prompt.
    RPROMPT=${rprompt}
}

prompt_ab_setup "$@"
